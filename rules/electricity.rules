rule "Time of lower price electricity start weekdays"
when
	Time cron "0 0 22 ? * MON-FRI"
then
	sendCommand(LowElectricity, ON)
	logInfo("electricity", "ZaÄetek poceni elektrike - " + LowElectricity.state)
end	

rule "Time of lower price electricity end weekdays"
when
	Time cron "0 0 6 ? * MON-FRI"
then
	sendCommand(LowElectricity, OFF)
	logInfo("electricity", "Konec poceni elektrike - " + LowElectricity.state)
end
 
rule "Set things up"
when
	System started
then
	//setting the price of electricity
	logInfo("electricity", "Ura je - " + now.toString)
	if (now.getHourOfDay > 6 && now.getHourOfDay < 22 && (now.getDayOfWeek != 6 && now.getDayOfWeek != 7)) {
		sendCommand(LowElectricity, OFF)
	} else {
		sendCommand(LowElectricity, ON)
	}
	logInfo("electricity", "Poceni elektrika je - " + LowElectricity.state)
end

rule "Calculate hourly electricity consumption"
when
	Time cron "5 0 * * * ?"
then
	logInfo("electricity", "#####################Starting calculations for hourly electricity consumption values")
	Consumption.members.forEach[ loopCons |
		logInfo("electricity", "Calculating consumption for " + loopCons.toString)
		val calcCons = CalcConsHour.members.filter(calcCons | calcCons.name.containsIgnoreCase(loopCons.name.substring(4,7))).head
		if (calcCons !== null) {
			logInfo("electricity", "The calculated item is " + calcCons.toString)
			var time = now.minusHours(1)
			logInfo("electricity", "Calculating from " + time)
			var tempCons = loopCons.deltaSince(now.minusHours(1).minusSeconds(6)) as Number
			logInfo("electricity", "The delta consumption is " + tempCons)
			if (tempCons == null) tempCons = 0
			postUpdate(calcCons, tempCons)
			calcCons.persist
			logInfo("electricity", "Saved the calculated consumption " + calcCons.toString)
		}
	]
	logInfo("electricity", "#####################Ended calculations for hourly electricity consumption values")
end

rule "Calculate daily electricity consumption"
when
	Time cron "5 0 0 * * ?"
then
	logInfo("electricity", "#####################Starting calculations for daily electricity consumption values")
	Consumption.members.forEach[ loopCons |
		logInfo("electricity", "Calculating consumption for " + loopCons.toString)
		val calcCons = CalcConsDay.members.filter(calcCons | calcCons.name.containsIgnoreCase(loopCons.name.substring(4,7))).head
		if (calcCons !== null) {
			logInfo("electricity", "The calculated item is " + calcCons.toString)
			var time = now.minusDays(1)
			logInfo("electricity", "Calculating from " + time)
			var tempCons = loopCons.deltaSince(now.minusDays(1).minusSeconds(6)) as Number
			logInfo("electricity", "The calculated consumption is " + tempCons)
			if (tempCons == null) tempCons = 0
			postUpdate(calcCons, tempCons)
			calcCons.persist
			logInfo("electricity", "Saved the calculated consumption " + calcCons.toString)
		}
	]
	logInfo("electricity", "#####################Ended calculations for daily electricity consumption values")
end

rule "Set all necessary variables for electricity"
when
	System started
then
	logDebug("electricity", "Setting up all needed variables!")
	CalcConsDay.members.forEach[ item |
		logDebug("electricity", "Setting up item " + item)
		if (item.state == NULL) item.postUpdate(0.0)
		logDebug("electricity", "Item set up " + item.toString)
	]
	CalcConsHour.members.forEach[ item |
		logDebug("electricity", "Setting up item " + item)
		if (item.state == NULL) item.postUpdate(0.0)
		logDebug("electricity", "Item set up " + item.toString)
	]
	logDebug("electricity", "Needed variables all set up")
end